* Encoding: UTF-8.

***** Descriptive Statistics *****.
MEANS TABLES=DV1
  /CELLS=COUNT MEAN STDDEV VAR SEMEAN.

MEANS TABLES=DV1 BY IV2
  /CELLS=COUNT MEAN STDDEV VAR SEMEAN.

***** T Tests (One Sample) *****.
T-TEST
  /TESTVAL=5
  /MISSING=ANALYSIS
  /VARIABLES=DV1
  /CRITERIA=CI(.95).

***** T Tests (Independent Samples) *****.
T-TEST GROUPS=IV1('Friend' 'Stranger')
  /MISSING=ANALYSIS
  /VARIABLES=DV1
  /CRITERIA=CI(.95).

***** T Tests (Paired Samples) *****.
T-TEST PAIRS=DV1 WITH DV2 (PAIRED)
  /CRITERIA=CI(.9500)
  /MISSING=ANALYSIS.

***** ANOVA (Main Effects & Interaction Effects) *****.
GLM DV1 DV2
  /WSFACTOR=Within 2 Polynomial 
  /METHOD=SSTYPE(3)
  /CRITERIA=ALPHA(.05)
  /WSDESIGN=Within.

UNIANOVA DV1 BY IV1 IV2
  /METHOD=SSTYPE(3)
  /INTERCEPT=INCLUDE
  /CRITERIA=ALPHA(0.05)
  /DESIGN=IV1 IV2 IV1*IV2.

GLM DV1 DV2 BY IV1 IV2
  /WSFACTOR=Within 2 Polynomial 
  /METHOD=SSTYPE(3)
  /CRITERIA=ALPHA(.05)
  /WSDESIGN=Within 
  /DESIGN=IV1 IV2 IV1*IV2.

***** ANOVA (Planned Contrasts) *****.
autorecode variables = IV2
 /into IV2recode.

ONEWAY DV1 BY IV2recode
  /CONTRAST=-1 1 0 
  /MISSING ANALYSIS.

ONEWAY DV1 BY IV2recode
  /CONTRAST=-1 0.5 0.5
  /MISSING ANALYSIS.

***** ANOVA (Simple Main Effects) *****.
DATASET ACTIVATE DataSet1.
UNIANOVA DV1 BY IV1 IV2
  /METHOD=SSTYPE(3)
  /INTERCEPT=INCLUDE
  /EMMEANS=TABLES(IV1*IV2)  COMPARE(IV1)
  /CRITERIA=ALPHA(0.05)
  /DESIGN=IV1 IV2 IV1*IV2.

***** Linear Regression *****.
COMPUTE DV1xDV2 = DV1 * DV2.
REGRESSION
  /MISSING LISTWISE
  /STATISTICS COEFF OUTS R ANOVA
  /CRITERIA=PIN(.05) POUT(.10)
  /NOORIGIN 
  /DEPENDENT DV3
  /METHOD=ENTER DV1 DV2 DV1xDV2.

***** Correlation Tests *****.
CORRELATIONS
  /VARIABLES=DV1 DV2
  /PRINT=TWOTAIL NOSIG
  /MISSING=PAIRWISE.

***** Chi-Square Tests *****.
autorecode variables = DV9
 /into DV9recode.
NPAR TESTS
  /CHISQUARE=DV9recode
  /EXPECTED=EQUAL
  /MISSING ANALYSIS.

CROSSTABS
  /TABLES=DV9 BY IV2
  /FORMAT=AVALUE TABLES
  /STATISTICS=CHISQ 
  /CELLS=COUNT
  /COUNT ROUND CELL.
